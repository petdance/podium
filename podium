#!/usr/bin/perl -w

use 5.10.0;

=for TODO

Add --version

Add "clean" action

Add "build" action

Let it copy over static stuff.

=cut

use strict;
use warnings;

use Carp::Always;
use Getopt::Long;
use YAML qw( LoadFile );
use Template ();
use Template::Constants qw( :debug :chomp );

use App::Podium;

MAIN: {
    my $configfile;

    GetOptions( 'configfile:s' => \$configfile ) or exit;

    if ( !$configfile ) {
        $configfile = 'config.yaml';
        say "Using $configfile";
    }

    my $config = LoadFile( $configfile );

    my %defaults = (
        INCLUDE_PATH => [ $config->{templatepath} ],
        OUTPUT_PATH  => $config->{buildpath},
        DEBUG        => DEBUG_UNDEF,
        TRIM         => CHOMP_ALL,
        PRE_CHOMP    => 1,
        POST_CHOMP   => 1,
    );

    my $tt = Template->new( \%defaults );
    my $vars = {};

    my @podfiles;
    my @sidelinks;

    my $podpath = $config->{podpath};
    for ( App::Podium::get_pages( $podpath, $config->{pages} ) ) {
        my ($sectionfile, $sectiontext) = @{$_};

        my $podfile  = "$podpath/$sectionfile.pod";
        my $htmlfile = "$sectionfile.html";
        push( @sidelinks, {
                filename => $htmlfile,
                text     => $sectiontext,
            } );
        push( @podfiles, {
                section  => $sectiontext,
                podfile  => $podfile,
                htmlfile => $htmlfile,
            } );
    }

    for my $vars ( @podfiles ) {
        $vars->{content} = App::Podium::pod2html( $vars->{podfile} );
        $vars->{sidelinks} = \@sidelinks;
    $tt->process( 'page.ttml', $vars, $vars->{htmlfile} ) || die $tt->error;
    }
}

=head1 AUTHOR

Andy Lester, C<< <andy at petdance.com> >>

=head1 BUGS

Please report any bugs or feature requests to the issues list at
Github: L<http://github.com/petdance/ack/issues>.  I will be notified,
and then you'll automatically be notified of progress on your bug
as I make changes.

=head1 SUPPORT

=over 4

=item * Bugs

L<http://github.com/petdance/ack/issues>

=item * AnnoCPAN: Annotated CPAN documentation

L<http://annocpan.org/dist/podium>

=item * CPAN Ratings

L<http://cpanratings.perl.org/d/podium>

=item * Search CPAN

L<http://search.cpan.org/dist/podium/>

=back

=head1 ACKNOWLEDGEMENTS

I would love to populate this section.  Please send me your contributions.

=head1 COPYRIGHT & LICENSE

Copyright 2010 Andy Lester

This program is free software; you can redistribute it and/or modify
it under the terms of either:

=over 4

=item * the GNU General Public License as published by the Free
Software Foundation; either version 1, or (at your option) any later
version, or

=item * the Artistic License version 2.0.

=back

=cut
