#!/usr/bin/perl -w

=for TODO

Add --version

=cut

use strict;
use warnings;

use Carp::Always;
use Getopt::Long;
use File::Slurp;
use Pod::Simple 3.13;
use YAML qw( LoadFile );

use Template ();
use Template::Constants qw( :debug :chomp );

use App::Podium::PSH;

my $podpath;
my $buildpath;

GetOptions(
    'podpath:s'   => \$podpath,
    'buildpath:s' => \$buildpath,
) or exit;

$podpath or die "Must specify --podpath\n";
$buildpath or die "Must specify --buildpath\n";

if ( ! -d $buildpath || ! -w $buildpath ) {
    die "Buildpath ($buildpath) doesn't exist or is not writable";
}

my %defaults = (
    INCLUDE_PATH => [ qw( tt ) ],
    OUTPUT_PATH  => $buildpath,
    DEBUG        => DEBUG_UNDEF,
    TRIM         => CHOMP_ALL,
    PRE_CHOMP    => 1,
    POST_CHOMP   => 1,
);

my $tt = Template->new( \%defaults );
my $vars = {};

my @podfiles;
my @sidelinks;

for ( get_sections( $podpath ) ) {
    my ($sectionfile, $sectiontext) = @{$_};

    my $podfile  = "$podpath/$sectionfile.pod";
    my $htmlfile = "$sectionfile.html";
    push( @sidelinks, {
        filename => $htmlfile,
        text     => $sectiontext,
    } );
    push( @podfiles, {
        section  => $sectiontext,
        podfile  => $podfile,
        htmlfile => $htmlfile,
    } );
}

for my $vars ( @podfiles ) {
    $vars->{content} = App::Podium::pod2html( $vars->{podfile} );
    $vars->{sidelinks} = \@sidelinks;
    $tt->process( 'page.ttml', $vars, $vars->{htmlfile} ) || die $tt->error;
}

sub get_sections {
    my $sourcedir = shift;

    opendir my $dh, $sourcedir or die "Can't open $sourcedir";
    my %pod = map { ($_,1) } grep { /\.pod$/ && -f "$sourcedir/$_" } readdir $dh;

    my $config = LoadFile( 'config.yaml' );
    my @sections = @{$config->{pages}};
    my @ordered_sections;
    for my $section ( @sections ) {
        my $filename = make_filename( $section );
        delete $pod{"$filename.pod"} or die "$filename is in the section list but not in the source";
        push( @ordered_sections, [ $filename, $section ] );
    }
    if ( keys %pod ) {
        die "The following pod files still exist: ", join( ', ', sort keys %pod );
    }

    return @ordered_sections;
}

sub make_filename {
    my $name = lc shift;

    $name =~ s/[^-a-z]+/-/g;
    $name =~ s/^-//;
    $name =~ s/-$//;

    return $name;
}

=head1 AUTHOR

Andy Lester, C<< <andy at petdance.com> >>

=head1 BUGS

Please report any bugs or feature requests to the issues list at
Github: L<http://github.com/petdance/ack/issues>.  I will be notified,
and then you'll automatically be notified of progress on your bug
as I make changes.

=head1 SUPPORT

You can find documentation for this module with the perldoc command.

    perldoc App::Podium

You can also look for information at:

=over 4

=item * Bugs

L<http://github.com/petdance/ack/issues>

=item * AnnoCPAN: Annotated CPAN documentation

L<http://annocpan.org/dist/podium>

=item * CPAN Ratings

L<http://cpanratings.perl.org/d/podium>

=item * Search CPAN

L<http://search.cpan.org/dist/podium/>

=back

=head1 ACKNOWLEDGEMENTS

I would love to populate this section.  Please send me your
contributions.

=head1 COPYRIGHT & LICENSE

Copyright 2010 Andy Lester, all rights reserved.

This program is free software; you can redistribute it and/or modify
it under the terms of either:

=over 4

=item * the GNU General Public License as published by the Free
Software Foundation; either version 1, or (at your option) any later
version, or

=item * the Artistic License version 2.0.

=back

=cut
